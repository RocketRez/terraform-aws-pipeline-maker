data "aws_iam_policy_document" "codepipeline_iam_policy_document" {
  statement {
    actions = ["sts:AssumeRole"]
    effect  = "Allow"
    principals {
      type        = "Service"
      identifiers = ["codepipeline.amazonaws.com"]
    }
  }
}

resource "aws_iam_role" "codepipeline_role" {
  name               = "codepipeline-role-${var.project_name}"
  assume_role_policy = data.aws_iam_policy_document.codepipeline_iam_policy_document.json
  path               = "/ci-cd-automated-roles/"
}

data "aws_codestarconnections_connection" "codestar_connection" {
  arn = var.codestar_arn
}

data "aws_iam_policy_document" "codepipeline_role_iam_policy_document" {
  statement {
    effect = "Allow"
    actions = [
      "s3:GetObject",
      "s3:GetObjectVersion",
      "s3:GetBucketVersioning",
      "s3:PutObjectAcl",
      "s3:PutObject"
    ]
    resources = [
      "${local.aws_s3_codepipeline_bucket[0].arn}/*",
      "${local.aws_s3_codepipeline_bucket[0].arn}"
    ]
  }
  statement {
    effect    = "Allow"
    actions   = ["codestar-connections:*"]
    resources = ["*"]
  }
  statement {
    effect = "Allow"
    actions = [
      "codebuild:BatchGetBuilds",
      "codebuild:StartBuild"
    ]
    resources = ["*"]
  }
  statement {
    effect = "Allow"
    actions = [
      "ecs:*",
      "iam:PassRole"
    ]
    resources = ["*"]
  }
}

resource "aws_iam_role_policy" "codepipeline_policy" {
  name   = "codepipeline_policy"
  role   = aws_iam_role.codepipeline_role.id
  policy = data.aws_iam_policy_document.codepipeline_role_iam_policy_document.json
}

